apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'checkstyle'
apply plugin: 'jacoco'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.springframework.boot'
apply plugin: 'com.gorylenko.gradle-git-properties'
apply plugin: 'net.researchgate.release'
apply from: project.buildscript.classLoader.getResource('releasable.gradle').toURI()

group = 'tech.viacom.arc-ng'

sourceCompatibility = 1.8
targetCompatibility = 1.8

buildscript {
    ext.arcNgDependenciesVersion = '1.0.61'
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            credentials {
                username = mtvnproximityUser
                password = mtvnproximityPassword
            }
            url = "http://nexus.mtvi.com/nexus/content/groups/public"
        }
    }
    dependencies {
        classpath "net.researchgate:gradle-release:2.6.0"
        classpath "gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:1.4.17"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:2.0.0.M3"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.3.RELEASE"
        classpath "tech.viacom.arc-ng:arc-ng-dependencies:$arcNgDependenciesVersion"
    }
}

dependencyManagement {
    imports {
        mavenBom("tech.viacom.arc-ng:arc-ng-dependencies:$arcNgDependenciesVersion")
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

sourceSets {
    integrationTest {
        groovy.srcDir file('src/test-integration/groovy')
        java.srcDir file('src/test-integration/java')
        resources.srcDir file('src/test-integration/resources')
        compileClasspath = sourceSets.main.output + sourceSets.test.output + configurations.integrationTestCompile
        runtimeClasspath = output + compileClasspath + configurations.integrationTestRuntime
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        credentials {
            username = mtvnproximityUser
            password = mtvnproximityPassword
        }
        url = "http://nexus.mtvi.com/nexus/content/groups/public"
    }
}

dependencies {
    compile 'org.springframework.cloud:spring-cloud-starter-eureka-server'
    compile 'org.springframework.cloud:spring-cloud-starter-config'
    compile 'org.springframework.cloud:spring-cloud-starter-aws'
    compile 'org.springframework.cloud:spring-cloud-aws-actuator'
}

checkstyle {
    configFile rootProject.file('checkstyle.xml')
}

jacocoTestReport {
    group = "Reporting"
    reports {
        xml.enabled true
        csv.enabled false
        html.destination file("${buildDir}/reports/coverage")
    }
    dependsOn test
}

tasks.withType(JavaCompile) {
    options.compilerArgs += '-parameters'
}

processResources {
    filesMatching("**/application.properties") {
        expand(project.properties)
    }
}

bootRun {
    systemProperties System.properties
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.3'
}